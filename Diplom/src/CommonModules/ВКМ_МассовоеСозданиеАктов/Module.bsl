#Область ПрограмныйИнтерфейс    

// Создаем акты выполненных работ по договору "Абонентское обслуживание"
// Возвращаемое значение - массив записей
Функция СоздатьАктыПоДоговорамАбонентскойПлаты(Период, ДатаАктов) Экспорт  
	
	
	Если Не ЗначениеЗаполнено(Период) Тогда  
		ОбщегоНазначения.СообщитьПользователю("Не выбран период "); 
		Возврат Неопределено;
	КонецЕсли;
	
	// Ищем Договоры, которые не попадают в список договоров, по которым уже есть реализации. 
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка КАК Договор,
	|	ДоговорыКонтрагентов.Организация КАК Организация,
	|	ДоговорыКонтрагентов.ВКМ_СуммаАбонентскойПлаты КАК АбонентскаяПлата,
	|	ДоговорыКонтрагентов.ВКМ_СтоимостьЧасаРаботы КАК СтоимостьЧасаРаботыСпециалиста,
	|	ДоговорыКонтрагентов.Владелец КАК Контрагент
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.ВидДоговора = ЗНАЧЕНИЕ(ПЕРЕЧИСЛЕНИЕ.ВидыДоговоровКонтрагентов.АбонентскоеОбслуживание)
	|	И НЕ ДоговорыКонтрагентов.Ссылка В
	|		(ВЫБРАТЬ
	|			РеализацияТоваровУслуг.Договор КАК Договор
	|		ИЗ
	|			Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ГДЕ
	|			РеализацияТоваровУслуг.Дата МЕЖДУ &ДатаНачало И &ДатаКонец
	|			И
	|				РеализацияТоваровУслуг.Договор.ВидДоговора = ЗНАЧЕНИЕ(ПЕРЕЧИСЛЕНИЕ.ВидыДоговоровКонтрагентов.АбонентскоеОбслуживание))
	|	И ДоговорыКонтрагентов.ВКМ_НачалоДействияДоговора < &ДатаАкта
	|	И ДоговорыКонтрагентов.ВКМ_ОкончаниеДействияДоговора > &ДатаАкта";
	
	Запрос.УстановитьПараметр("ДатаАкта", ДатаАктов);	
	Запрос.УстановитьПараметр("ДатаНачало", Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаКонец", Период.ДатаОкончания);
	
	Выборка = Запрос.Выполнить().Выбрать();  
	
	СозданныеАкты = Новый Массив;  
	// И по этим договорам создаем и заполняем Акты
	Пока Выборка.Следующий() Цикл 
		НовыйАкт = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
		НовыйАкт.Дата = ДатаАктов;
		НовыйАкт.Организация = Выборка.Организация;
		НовыйАкт.Контрагент = Выборка.Контрагент;
		НовыйАкт.Договор = Выборка.Договор;
		НовыйАкт.ВКМ_ЗаполнитьНаСервере(НовыйАкт); 
		НовыйАкт.Записать(РежимЗаписиДокумента.Проведение,РежимПроведенияДокумента.Неоперативный);
		
		Запись = Новый Структура;
		Запись.Вставить("Ссылка", НовыйАкт.Ссылка);
		Запись.Вставить("Договор",НовыйАкт.Договор);
		СозданныеАкты.Добавить(Запись); 	
		
	КонецЦикла;  
	
	Возврат ?(ЗначениеЗаполнено(СозданныеАкты) , СозданныеАкты, Неопределено);	
	
КонецФункции

#КонецОбласти 




